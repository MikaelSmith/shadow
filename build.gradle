import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.1'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.9.9"
        classpath "com.gradle.publish:plugin-publish-plugin:0.11.0"
        classpath 'org.ajoberstar:grgit:2.1.1'
        classpath 'org.ajoberstar:gradle-git-publish:0.3.3'
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    }
}

plugins {
    id 'groovy'
    id 'project-report'
    id 'idea'
    id 'java-gradle-plugin'
}

// Remove the gradleApi so it isn't merged into the jar file.
configurations.named(JavaPlugin.API_CONFIGURATION_NAME) {
    dependencies.remove(project.dependencies.gradleApi())
}

group = 'com.github.jengelman.gradle.plugins'
version = file('src/main/resources/shadow-version.txt').text.trim()

gradlePlugin {
    plugins{
        shadowPlugin {
            id = 'com.github.johnrengelman.shadow'
            displayName = 'Shadow Plugin'
            implementationClass = 'com.github.jengelman.gradle.plugins.shadow.ShadowPlugin'
        }
    }
}

apply plugin: 'com.github.johnrengelman.shadow'

apply from: file('gradle/dependencies.gradle')

repositories {
    gradlePluginPortal()
    mavenCentral()
}

test {
    if (System.env.CI == 'true') {
        testLogging.showStandardStreams = true
        minHeapSize "1g"
        maxHeapSize "1g"
    }
    
    systemProperty 'java.io.tmpdir', buildDir.absolutePath
}

jar {
    from rootProject.file('LICENSE')
    from rootProject.file('NOTICE')
}

shadowJar {
    classifier = ''
}

idea {
    project {
        languageLevel = '1.8'
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

task downloadDependencies(type: Exec) {
    dependsOn configurations.testRuntime
    commandLine 'echo', 'Downloaded all dependencies'
}

tasks.build.dependsOn tasks.shadowJar

project.tasks.withType(JavaCompile) {
    // This will be the default in Gradle 5.0
    if (!options.compilerArgs.contains("-processor")) {
        options.compilerArgs << '-proc:none'
    }
}

project.tasks.withType(GroovyCompile) {
    // This will be the default in Gradle 5.0
    if (!options.compilerArgs.contains("-processor")) {
        options.compilerArgs << '-proc:none'
    }
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar
